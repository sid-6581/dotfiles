#!/usr/bin/env nu

# Relaunch using Windows nushell if started from WSL.
if $nu.os-info.name == "linux" {
  let nu_exe = $"($env.HOME)/winhome/.local/bin/nu.exe"

  if not ($nu_exe | path exists) {
    exit
  }

  ^$nu_exe $env.CURRENT_FILE
  exit
}

use nushell/scripts/globals.nu
use nushell/scripts/log.nu
use nushell/scripts/windows.nu
use nushell/scripts/setup
use nushell/scripts/nu-install *

const category = path self | path basename

log info -c $category "Starting"

# Run everything from the Windows home directory, since some tools don't like being run from the WSL UNC path.
cd $env.HOME

setup git author

windows set-user-env {
  EDITOR:                  "neovide"
  GIT_AUTHOR_EMAIL:        $env.GIT_AUTHOR_EMAIL?
  GIT_AUTHOR_NAME:         $env.GIT_AUTHOR_NAME?
  GIT_COMMITTER_EMAIL:     $env.GIT_COMMITTER_EMAIL?
  GIT_COMMITTER_NAME:      $env.GIT_COMMITTER_NAME?
  HOME:                    $env.HOME?
  NODE_UNC_HOST_ALLOWLIST: "wsl.localhost"
  Path: (
    [
      ($env.HOME | path join .local bin)
      ($env.HOME | path join .cargo bin)
      ($env.HOME | path join .local share bob nvim-bin)
      ($env.HOME | path join scoop shims)
      ...(
        registry query --hkcu Environment Path
        | get value
        | split row (char esep)
      )
    ]
    | compact -e
    | path expand --no-symlink
    | uniq
    | str join (char esep)
  )
  RIPGREP_CONFIG_PATH:     $env.RIPGREP_CONFIG_PATH?
  WSLHOME:                 (path self .)
  XDG_CACHE_HOME:          $env.XDG_CACHE_HOME?
  XDG_CONFIG_HOME:         $env.XDG_CONFIG_HOME?
  XDG_DATA_HOME:           $env.XDG_DATA_HOME?
  XDG_STATE_HOME:          $env.XDG_STATE_HOME?
  YAZI_FILE_ONE:           ($env.HOME | path join scoop apps git current usr bin file.exe)
}

# Pull latest changes from our github repo first.
nu-install git [
  { repo: "https://github.com/sid-6581/dotfiles", dir: ($env.HOME | path join .dotfiles) }
]

nu-install link clean

nu-install link [
  { target: ($env.HOME | path join .dotfiles config), link: $env.HOME  }
  { target: ($env.HOME | path join .dotfiles config-windows), link: $env.HOME  }
  { target: ($env.HOME | path join .dotfiles config .config yazi), link: ($env.HOME | path join AppData Roaming yazi config) }
]

# Remove scoop application that may have been installed before.
nu-install scoop uninstall --apps [
  neovim
  neovim-nightly
]

nu-install winget [
  Microsoft.PowerShell
  AutoHotkey.AutoHotkey
]

nu-install scoop --buckets [
  extras
  versions
  nerd-fonts
] --apps [
  JetBrainsMono-NF
  bat
  broot
  carapace-bin
  cmake
  delta
  fd
  fzf
  gh
  git
  jq
  just
  lazygit
  llvm
  lsd
  nodejs
  packer
  pnpm
  python
  ripgrep
  sed
  starship
  sudo
  sysinternals
  tealdeer
  uutils-coreutils
  wezterm-nightly
  which
  win32yank
  xsv
  yazi
  zoxide
]

^sudo.cmd $"($env.HOME)/.local/bin/nu" $"(path self .)/install-windows-sudo.nu"

nu-install git [
  { repo: "https://github.com/sid-6581/dotfiles", dir: ($env.HOME | path join .dotfiles) }
  { repo: "https://github.com/sid-6581/NeovimConfig", dir: ($env.HOME | path join .config nvim) }
]

if (which rustup | is-empty) {
  if (^winget list --accept-source-agreements | find "VisualStudio.202" | is-empty) {
    log warning "Visual Studio not installed, skipping installation of rustup"
  } else {
    nu-install winget [ Rustlang.Rustup ]
    ^rustup -q toolchain install nightly
  }
}

nu-install cargo [
  cargo-edit
  cargo-sweep
  cargo-update
  x12pp
]

nu-install gh [
  { repo: "nushell/nushell", pattern: "*x86_64-pc-windows-msvc.zip" } # nushell - Shell
  { repo: "rust-lang/rust-analyzer", pattern: "rust-analyzer-x86_64-pc-windows-msvc.zip" } # rust-analyzer - LSP for rust
  { repo: "MordechaiHadad/bob", pattern: "bob-windows-x86_64.zip"  } # bob - Neovim version manager
]

if (which bob | is-empty) {
  log warning -c $category "bob not installed, skipping installation of neovim"
} else if (^bob list | find used | is-empty) {
  log info -c $category "Installing neovim"
  ^bob use nightly
}

if not ($"($env.HOME)/.wslconfig" | path exists) {
  "[wsl2]\nkernelCommandLine = cgroup_no_v1=all\nnetworkingMode = nat" | save -f ($env.HOME | path join .wslconfig)
}
