#!/usr/bin/env nu

# Relaunch using Windows nushell if started from WSL.
if $nu.os-info.name == "linux" {
  let nu_exe = $"($env.HOME)/winhome/.local/bin/nu.exe"

  if not ($nu_exe | path exists) {
    exit
  }

  ^$nu_exe $env.CURRENT_FILE
  exit
}

use nushell/scripts/globals.nu
use nushell/scripts/log.nu
use nushell/scripts/setup

$env.LOG_CATEGORY = "update-windows"

log info "Starting"

# Run everything from the Windows home directory, since some tools don't like being run from the WSL UNC path.
cd $env.HOME

setup update app scoop {
  ^scoop update
  ^scoop update -s '*'
  ^scoop cleanup '*'
  ^scoop cache rm '*'
  try { ^git config --system --unset credential.helper }
  ^sudo scoop update -g -s '*'
}

setup update app rustup { ^rustup update }
setup update app cargo { ^cargo install-update -a }
setup update app tldr { ^tldr -u }
setup update app bob { ^bob use nightly }
